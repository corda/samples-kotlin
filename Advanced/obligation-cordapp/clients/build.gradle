plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'io.spring.dependency-management'
}

sourceSets {
    main {
        resources {
            srcDir "../config/dev"
        }
    }
}

// See https://docs.spring.io/dependency-management-plugin/docs/current/reference/html
dependencyManagement {
    dependencies {
        dependency "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "junit:junit:$junit_version"

    // Corda dependencies.
    implementation "net.corda:corda-core:$corda_release_version"
    implementation "net.corda:corda-jackson:$corda_release_version"
    implementation "net.corda:corda-rpc:$corda_release_version"

    implementation("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    implementation "org.springframework.boot:spring-boot-starter-log4j2:$spring_boot_version"
    implementation "org.apache.logging.log4j:log4j-web:$log4j_version"

    implementation project(':workflows')
    implementation project(':contracts')
    implementation "$corda_core_group:corda-finance-contracts:$corda_release_version"
    implementation "$corda_release_group:corda-finance-workflows:$corda_release_version"
}

tasks.register('runPartyAServer', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.obligation.server.ServerKt'
    args '--server.port=10009', '--config.rpc.host=localhost', '--config.rpc.port=10008', '--config.rpc.username=user1', '--config.rpc.password=password'
}

tasks.register('runPartyBServer', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.obligation.server.ServerKt'
    args '--server.port=10012', '--config.rpc.host=localhost', '--config.rpc.port=10011', '--config.rpc.username=user1', '--config.rpc.password=password'
}

tasks.register('runPartyCServer', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.obligation.server.ServerKt'
    args '--server.port=10015', '--config.rpc.host=localhost', '--config.rpc.port=10014', '--config.rpc.username=user1', '--config.rpc.password=password'
}
