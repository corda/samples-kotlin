plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.springframework.boot'
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'

    // Corda dependencies.
    implementation "$corda_release_group:corda-rpc:$corda_release_version"

    // CorDapp dependencies.
    implementation project(':contracts')
    implementation project(':workflows')
    implementation("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    runtimeOnly "org.apache.logging.log4j:log4j-web:${log4j_version}"

    // SendGrid
    implementation 'com.sendgrid:sendgrid-java:4.7.0'
}


springBoot {
    mainClassName = "net.corda.samples.secretsanta.webserver.ServerKt"
}

/* The Client is the communication channel between the external and the node. This task will help you immediately
 * execute your rpc methods in the main method of the client.kt. You can somewhat see this as a quick test of making
 * RPC calls to your nodes.
 */
tasks.register('runSantaClient', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.secretsanta.ClientKt'
    args 'localhost:10016', 'user1', 'test'
}

/* This task will start the springboot server that connects to your node (via RPC connection). All of the http requests
 * are in the Controller file. You can leave the Server.kt and NodeRPCConnection.kt file untouched for your use.
 */
tasks.register('runSantaServer', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.secretsanta.webserver.ServerKt'
    args '--server.port=10056', '--config.rpc.host=localhost', '--config.rpc.port=10016', '--config.rpc.username=user1', '--config.rpc.password=test'
}
