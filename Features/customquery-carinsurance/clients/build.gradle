repositories {
    mavenLocal()

    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/exposed' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://download.corda.net/maven/corda-releases' }
}

apply plugin: 'kotlin'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

sourceSets {
    main {
        resources {
            srcDir "../config/dev"
        }
    }
}

// See https://docs.spring.io/dependency-management-plugin/docs/current/reference/html
dependencyManagement {
    dependencies {
        dependency "org.apache.logging.log4j:log4j-slf4j2-impl:$log4j_version"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "junit:junit:$junit_version"

    // Corda dependencies.
    implementation "net.corda:corda-core:$corda_release_version"
    implementation "net.corda:corda-jackson:$corda_release_version"
    implementation "net.corda:corda-rpc:$corda_release_version"
    implementation "net.corda:corda:$corda_release_version"
    testImplementation "net.corda:corda-node-driver:$corda_release_version"

    implementation("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    implementation "org.springframework.boot:spring-boot-starter-log4j2:$spring_boot_version"
    implementation "org.apache.logging.log4j:log4j-web:$log4j_version"
    implementation "org.bouncycastle:bcprov-jdk18on:1.75"

    cordapp project(":workflows")
    cordapp project(":contracts")
}

task bootRun(type: JavaExec, dependsOn: jar) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.carinsurance.server.ServerKt'
    args '--server.port=8080', '--config.rpc.host=localhost', '--config.rpc.port=10006', '--config.rpc.username=user1', '--config.rpc.password=test'
}

// Note that the bootRun task is built into gradle so there is no webserver task defined here.
