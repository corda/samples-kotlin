plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.springframework.boot'
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
}

dependencies {
    // Corda dependencies.
    implementation "$corda_release_group:corda-rpc:$corda_release_version"

    // CorDapp dependencies.
    implementation project(':contracts')
    implementation project(':workflows')
    implementation project(path: ':contracts', configuration: 'testArtifacts')

    implementation("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    runtimeOnly "org.apache.logging.log4j:log4j-web:$log4j_version"
    runtimeOnly "org.slf4j:jul-to-slf4j:$slf4j_version"
}

springBoot {
    mainClassName = "net.corda.samples.blacklist.webserver.Server"
}

tasks.register('uploadBlacklist', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.blacklist.clients.ClientKt'
    args 'localhost:10006', 'localhost:10009', 'localhost:10012'
}
